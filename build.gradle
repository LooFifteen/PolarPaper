import org.gradle.api.publish.maven.MavenPublication

plugins {
    id 'java'
    id 'maven-publish'

    id 'com.gradleup.shadow' version '8.3.5'
    id("io.papermc.paperweight.userdev") version "1.7.2"
}

group = 'live.minehub'
version = '1.0.0'

repositories {
    mavenCentral()
}

dependencies {
    paperweight.paperDevBundle("1.21.3-R0.1-SNAPSHOT")
    implementation 'com.github.luben:zstd-jni:1.5.6-8'
    implementation 'net.kyori:adventure-nbt:4.17.0'
}

tasks.assemble {
    dependsOn(tasks.named("shadowJar"))
    dependsOn(tasks.reobfJar)
}

publishing {
    repositories {
        // Publish to Maven Local only if not running in an action environment
        if (!isAction()) {
            mavenLocal()
        } else {
            maven {
                name = "GithubPackages"
                url = uri("https://maven.pkg.github.com/MinehubMC/PolarPaper")
                credentials {
                    username = System.getenv("GITHUB_ACTOR")
                    password = System.getenv("GITHUB_TOKEN")
                }
            }
        }
    }

    publications {
        shadow(MavenPublication) { publication ->
            from components.java
        }
    }
}

static boolean isAction() {
    return System.getenv("CI") != null
}